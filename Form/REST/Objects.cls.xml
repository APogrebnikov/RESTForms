<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Form.REST.Objects">
<Description>
Broker class to work with queries</Description>
<Super>Form.REST.Abstract</Super>
<TimeCreated>64090,79332.572705</TimeCreated>

<Parameter name="ALL">
<Description>
Query to return all availible information about form objects</Description>
<Default>*</Default>
</Parameter>

<XData name="UrlMap">
<XMLNamespace>http://www.intersystems.com/urlmap</XMLNamespace>
<Data><![CDATA[
<Routes>
<Route Url="/:form/:query" Method="GET" Call="getFormObjects"/>
</Routes>
]]></Data>
</XData>

<Method name="getFormObjects">
<Description>
Get all form objects</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%String="",queryType:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	set sc = ##class(Form.Info).formExistsStatus(class)
	return:$$$ISERR(sc) sc
	set sc = ..checkObjPermission(class, "R")
	return:$$$ISERR(sc) sc
	set sc = ..getBaseQuery(queryType, class, .queryBase)
	return:$$$ISERR(sc) sc	
	
	set table = $$$ClassSQLTable(class)
	set sql = queryBase _ table
	
	set sc = ##class(Form.JSON.SQL).WriteJSONFromSQL(sql, class)
	return sc
]]></Implementation>
</Method>

<Method name="getBaseQuery">
<ClassMethod>1</ClassMethod>
<FormalSpec>queryType:%String,class:%String,*queryBase:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set queryBase = ""
	set queryType = $ZCVT(queryType, "U")
	#dim sc As %Status = $$$OK
	if ##class(%Dictionary.ParameterDefinition).IDKEYExists($classname(), queryType) {
		/// we want only parameters defined in this class
		set queryBase = $parameter(, queryType)
	} elseif ##class(%Dictionary.MethodDefinition).IDKEYExists($classname(), "query" _ queryType) {
		set queryBase = $classmethod(, "query" _ queryType, class)
	} elseif 0 {
		/// TODO Some other type of "get stuff between SELECT and FROM"
		/// some callback class maybe
	} else {
		set sc = $$$ERROR($$$GeneralError, "Query type " _ queryType _ " not found")		
	}
	
	if (queryBase'="") {
		set queryBase = "SELECT " _ queryBase _ " FROM "
	}
	
	return sc
]]></Implementation>
</Method>

<Method name="queryINFO">
<Description>
Return id and dislpayName
w ##class(Form.REST.Objects).queryINFO("Form.TestForm")</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	set sqlProc = $$$ClassSQLTable(class) _ "_getObjectDisplayName(id)"
	set queryBase = "id AS _id, " _ sqlProc _ " As displayName"
	return queryBase
]]></Implementation>
</Method>
</Class>
</Export>
